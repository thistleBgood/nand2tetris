// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16 (in=lastOut0, out=lastOut1);
    Mux16 (a=lastOut0, b=lastOut1, sel=inc, out=incOut);
    Mux16 (a=incOut, b=in, sel=load, out=loadOut);
    Mux16 (a=loadOut, sel=reset, out=resetOut);
    Or8Way (in[0]=load, in[1]=inc, in[2]=reset, out=update);

    Register (in=resetOut, load=update, out=lastOut0, out=out);
}
